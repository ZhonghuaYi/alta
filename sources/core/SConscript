import os
import sys

Import('env', 'ALTA_LIBS')

# Library sources
sources = ['common.cpp',
           'data.cpp',
           'data_storage.cpp',
           'function.cpp',
           'params.cpp',
           'plugins_manager.cpp',
           'rational_function.cpp',
           'vertical_segment.cpp',
           'metrics.cpp']

CCFLAGS = env['CCFLAGS']

## Known compilation issue. If the core is not compiled with -fvisibility-hidden
## on OSX, the shared libraries and the softwares cannot share vtables. This
## impact the use of dynamic_cast in shared libraries.
##
if sys.platform == 'darwin':
	CCFLAGS = CCFLAGS + ' -fvisibility=hidden'

if env['CC'] != 'cl':
	# We're not building a real static library, but rather what
	# Libtool calls a "convenience library": an archive that
	# contains PIC code.  Thus, force '-fPIC'.
	CCFLAGS = CCFLAGS + ' -fPIC'

env.StaticLibrary('../build/core', source = sources,
                  LIBS = ALTA_LIBS,
                  CPPPATH = env['CPPPATH'] + env['EIGEN_INC'],
                  CCFLAGS = CCFLAGS)
