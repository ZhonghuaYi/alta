import SCons.Warnings as W

Import('env', 'library_available')
env = env.Clone()

build_lib = library_available(env, pkgspec='ceres',
                              inc_var='CERES_INC', lib_var='CERES_DIR',
                              lib='CERES_LIB',
                              header='ceres/ceres.h')

targets = []
if build_lib:
  conf = Configure(env)
  libs = []
  for lib in env['CERES_OPT_LIB']:
    if conf.CheckLib(lib, language='c++'):
      libs.append(lib)
  env = conf.Finish()

  env.AppendUnique(LIBS = ['core'])
  env.AppendUnique(LIBS = libs)

  targets = env.SharedLibrary('#build/plugins/nonlinear_fitter_ceres', 'fitter.cpp')

Return('targets')
